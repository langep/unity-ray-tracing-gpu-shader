// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture

// Data structure reprensenting a Ray
struct Ray
{
    float3 origin;
    float3 direction;
    float3 energy;
};

// Data structure to represent a RayHit
struct RayHit
{
    float3 position;
    float distance;
    float3 normal;
    float3 albedo;
    float3 specular;
    float smoothness;
    float3 emission;
};

// Data structure to represent a sphere
struct Sphere
{
    float3 position;
    float radius;
    float3 albedo;
    float3 specular;
    float smoothness;
    float3 emission;
};

//== Begin parameters passed in from the outside

// The output RenderTexture
RWTexture2D<float4> Result;
// The parameters
float4x4 _CameraToWorld;
float4x4 _CameraInverseProjection;
// The skybox texture
Texture2D<float4> _SkyboxTexture;
// The ground planes Y coordinate
float _GroundPlaneY;
// Pixel offset for progressive sampling
float2 _PixelOffset;
// The maximum number of reflections
int _Reflections;
// The directional light source with direction x, y, z and intensity w
float4 _DirectionalLight;
// The Spheres in the scene
StructuredBuffer<Sphere> _Spheres;
// Global Illumination
float _GlobalIllumination;

//== End parameters passed in from the outside


// Constants
static const float PI = 3.14159265f;

// Sampler for skybox texture
SamplerState sampler_SkyboxTexture;

// Randomness
float2 _Pixel;
float _Seed;

// Produces somewhat random float
float rand()
{
    float result = frac(sin(_Seed / 100.0f * dot(_Pixel, float2(12.9898f, 78.233f))) * 43758.5453f);
    _Seed += 1.0f;
    return result;
}

// Returns dot product scaled with optional factor f and clapmed to [0, 1]
float sdot(float3 x, float3 y, float f = 1.0f)
{
    return saturate(dot(x, y) * f);
}

// Function to construct a `Ray`
Ray CreateRay(float3 origin, float3 direction)
{
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;
    ray.energy = float3(1.0f, 1.0f, 1.0f);
    return ray;
}

// Create a Ray from the camera going in the direction `uv`
Ray CreateCameraRay(float2 uv)
{
    // Transform the camera origin to world space
    float3 origin = mul(_CameraToWorld, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;

    // Invert the perspective projection of the view-space position
    float3 direction = mul(_CameraInverseProjection, float4(uv, 0.0f, 1.0f)).xyz;
    // Transform the direction from camera to world space and normalize
    direction = mul(_CameraToWorld, float4(direction, 0.0f)).xyz;
    direction = normalize(direction);

    return CreateRay(origin, direction);
}

// Function to create default ray hit (no hit)
RayHit CreateRayHit()
{
    RayHit hit;
    hit.position = float3(0.0f, 0.0f, 0.0f);
    hit.distance = 1.#INF;
    hit.normal = float3(0.0f, 0.0f, 0.0f);
    hit.albedo = float3(0.0f, 0.0f, 0.0f);
    hit.specular = float3(0.0f, 0.0f, 0.0f);
    hit.smoothness = 0;
    hit.emission = float3(0.0f, 0.0f, 0.0f);
    return hit;
}

// Compute intersection of ray with ground plane at height y.
// Updates bestHit if ray direction is positive and the found hit
// is at a shorter distance than the previous best hit.
void IntersectGroundPlane(Ray ray, inout RayHit bestHit)
{
    // Calculate distance along the ray where the ground plane is intersected
    float t = -(ray.origin.y - _GroundPlaneY) / ray.direction.y;

    // Only update if direction is positive and closer than previous hit
    if (t > 0 && t < bestHit.distance)
    {
        bestHit.distance = t;
        bestHit.position = ray.origin + t * ray.direction;
        bestHit.normal = float3(0.0f, 1.0f, 0.0f);
        bestHit.albedo = 0.8f;
        bestHit.specular = 0.03f;
        bestHit.smoothness = 5;
        bestHit.emission = 0.0f;
    }
}

// Compute intersection of ray with a sphere
// Updates bestHit if ray direction is positive and the found hit
// is at a shorter distance than the previous best hit.
void IntersectSphere(Ray ray, inout RayHit bestHit, Sphere sphere)
{
    // Calculate distance along the ray where the sphere is intersected
    float3 d = ray.origin - sphere.position;
    float p1 = -dot(ray.direction, d);
    float p2sqr = p1 * p1 - dot(d, d) + sphere.radius * sphere.radius;
    if (p2sqr < 0)
        return;
    float p2 = sqrt(p2sqr);
    float t = p1 - p2 > 0 ? p1 - p2 : p1 + p2;
    if (t > 0 && t < bestHit.distance)
    {
        bestHit.distance = t;
        bestHit.position = ray.origin + t * ray.direction;
        bestHit.normal = normalize(bestHit.position - sphere.position);
        bestHit.albedo = sphere.albedo;
        bestHit.specular = sphere.specular;
        bestHit.smoothness = sphere.smoothness;
        bestHit.emission = sphere.emission;
    }
}

// Trace a ray to find the closest intersection
RayHit Trace(Ray ray)
{
    RayHit bestHit = CreateRayHit();
    IntersectGroundPlane(ray, bestHit);

// Trace spheres
    uint numSpheres, stride;
    _Spheres.GetDimensions(numSpheres, stride);
    for (uint i = 0; i < numSpheres; i++)
    {
        IntersectSphere(ray, bestHit, _Spheres[i]);
    }


    return bestHit;
}


float3x3 GetTangentSpace(float3 normal)
{
    // Choose a helper vector for the cross product
    float3 helper = float3(1, 0, 0);
    if (abs(normal.x) > 0.99f)
    {
        helper = float3(0, 0, 1);
    }

    // Generate vectors
    float3 tangent = normalize(cross(normal, helper));
    float3 binormal = normalize(cross(normal, tangent));
    return float3x3(tangent, binormal, normal);
}

// Samples uniformly from the hemisphere
// alpha = 0 for uniform
// alpha = 1 for cosine
// alpha > 1 for higher Phong exponents
float3 SampleHemisphere(float3 normal, float alpha)
{
    // Sample the hemisphere, where alpha determines the kind of the sampling
    float cosTheta = pow(rand(), 1.0f / (alpha + 1.0f));
    float sinTheta = sqrt(1.0f - cosTheta * cosTheta);
    float phi = 2 * PI * rand();
    float3 tangentSpaceDir = float3(cos(phi) * sinTheta, sin(phi) * sinTheta, cosTheta);
    // Transform direction to world space
    return mul(tangentSpaceDir, GetTangentSpace(normal));
}


// Converts direction from carthesian coords to spherical coords
float2 CarthesianToSpherical(float3 direction)
{
    float phi = atan2(direction.x, -direction.z) / -PI * 0.5f;
    float theta = acos(direction.y) / -PI;
    return float2(phi, theta);
}

// Average the color channels
float energy(float3 color)
{
    return dot(color, 1.0f / 3.0f);
}

// Convert smoothness value to Phong exponent
float SmoothnessToPhongAlpha(float s)
{
    return pow(1000.0f, s * s);
}

// Shade the pixel originating the ray based with
//  geometry was hit: the normal of the geometry at the hit location
//  otherwise: sample from skybox
// Also updates the ray with the correct relection direction and
// decreased energy.
float3 Shade(inout Ray ray, RayHit hit)
{
    if (hit.distance < 1.#INF)
    {
        float epsilon = 0.001f; // offset to prevent self-occlusion and self-shadowing
        // Calculate chances of diffuse and specular reflection
        hit.albedo = min(1.0f - hit.specular, hit.albedo);
        float specChance = energy(hit.specular);
        float diffChance = energy(hit.albedo);
        float sum = specChance + diffChance;
        specChance /= sum;
        diffChance /= sum;

        // Roulette-select the ray's path
        float roulette = rand();
        if (roulette < specChance)
        {
            // Specular reflection
            float alpha = SmoothnessToPhongAlpha(hit.smoothness);
            ray.origin = hit.position + hit.normal * epsilon;
            ray.direction = SampleHemisphere(reflect(ray.direction, hit.normal), alpha);
            float f = (alpha + 2) / (alpha + 1);
            ray.energy *= (1.0f / specChance) * hit.specular * sdot(hit.normal, ray.direction, f);
        }
        else if (diffChance > 0 && roulette < specChance + diffChance)
        {
            // Diffuse reflection
            ray.origin = hit.position + hit.normal * epsilon;
            ray.direction = SampleHemisphere(hit.normal, 1.0f);
            ray.energy *= (1.0f / diffChance) * 2 * hit.albedo * sdot(hit.normal, ray.direction);
        }
        else
        {
            // Terminate ray
            ray.energy = 0.0f;
        }
        return hit.emission;
    }
    else
    {
        // We hit the sky which reflects nothing
        ray.energy = 0.0f;

        int lod = 0;
        return _GlobalIllumination * _SkyboxTexture.SampleLevel(sampler_SkyboxTexture, CarthesianToSpherical(ray.direction), lod);
    }
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    // Set the current pixel coords for randomness computation
    _Pixel = id.xy;

    // Get the dimensions of the RenderTexture
    uint width, height;
    Result.GetDimensions(width, height);

    // Transform pixel to [-1,1] range and add random offset
    // for progressive sampling
    float2 uv = float2((id.xy + _PixelOffset) / float2(width, height) * 2.0f - 1.0f);

    // Get a ray for the UVs
    Ray ray = CreateCameraRay(uv);

    // Trace and shade
    float3 result = float3(0, 0, 0);
    for (int i = 0; i < _Reflections + 1; i++)
    {
        RayHit hit = Trace(ray);
        result += ray.energy * Shade(ray, hit);
        if (!any(ray.energy))
            break;
    }

    Result[id.xy] = float4(result, 1);
}
